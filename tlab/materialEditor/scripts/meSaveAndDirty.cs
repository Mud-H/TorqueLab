//==============================================================================
// TorqueLab ->
// Copyright (c) 2015 All Right Reserved, http://nordiklab.com/
//------------------------------------------------------------------------------
//==============================================================================
//==============================================================================
// Material Update Functionality
$Pref::MaterialEditorTools::DefaultMaterialFile = "art/materials.cs";
function MaterialEditorTools::saveDialogCancel( %this ) {
	MaterialEditorTools.guiSync( materialEd_previewMaterial );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialEditorTools::saveDialogDontSave( %this, %material ) {
	MaterialEditorTools.currentMaterial.setName( %this.originalName );
	//restore to defaults
	MaterialEditorTools.copyMaterials( notDirtyMaterial, MaterialEditorTools.currentMaterial );
	MaterialEditorTools.copyMaterials( notDirtyMaterial, materialEd_previewMaterial );
	MaterialEditorTools.guiSync( materialEd_previewMaterial );
	materialEd_previewMaterial.flush();
	materialEd_previewMaterial.reload();
	MaterialEditorTools.currentMaterial.flush();
	MaterialEditorTools.currentMaterial.reload();
	MaterialEditorTools.setMaterialNotDirty();
	MaterialEditorTools.setActiveMaterial( %material );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialEditorTools::saveDialogSave( %this, %material ) {
	MaterialEditorTools.save();
	MaterialEditorTools.setActiveMaterial( %material );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialEditorTools::save( %this,%material,%external ) {
	%mat = MaterialEditorTools.currentMaterial;

	if (!isObject(%mat))
		return;

	if( %mat.getName() $= "" ) {
		LabMsgOK("Cannot perform operation", "Saved materials cannot be named \"\". A name must be given before operation is performed" );
		return;
	}

	// Update the live object regardless in this case
	MaterialEditorTools.updateLivePreview(true);

	// Specifically for materials autogenerated from shapes.
	if( %mat.isAutoGenerated() )
		%mat.setAutoGenerated( false );
	%isDirty = matEd_PersistMan.isDirty(MaterialEditorTools.currentMaterial);
	// Save the material using the persistence manager
	matEd_PersistMan.saveDirty();
	// Clean up the Material Editor
	MaterialEditorTools.copyMaterials( materialEd_previewMaterial, notDirtyMaterial );
	MaterialEditorTools.setMaterialNotDirty();
}
//------------------------------------------------------------------------------
//MaterialEditorTools.setMaterialDirty
//==============================================================================
function MaterialEditorTools::setMaterialNotDirty(%this) {
	%propertyText = "Material Properties";
	%previewText = "Material Preview";
	MaterialEditorPropertiesWindow.text = %propertyText;
	MaterialEditorPreviewWindow.text = %previewText;
	MaterialEditorPropertiesWindow-->quickSaveButton.active = 0;
	MaterialEditorTools.materialDirty = false;
	matEd_PersistMan.removeDirty(MaterialEditorTools.currentMaterial);
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialEditorTools::setMaterialDirty(%this) {
	%propertyText = "Material Properties *";
	%previewText = "Material Preview *";
	MaterialEditorPropertiesWindow.text = %propertyText;
	MaterialEditorPreviewWindow.text = %previewText;
	MaterialEditorTools.materialDirty = true;
	MaterialEditorPropertiesWindow-->quickSaveButton.active = 1;

	// materials created in the material selector are given that as its filename, so we run another check
	if( MaterialEditorTools.isMatEditorMaterial( MaterialEditorTools.currentMaterial ) ) {
		if( MaterialEditorTools.currentMaterial.isAutoGenerated() ) {
			%obj = MaterialEditorTools.currentObject;

			if( %obj.shapeName !$= "" )
				%shapePath = %obj.shapeName;
			else if( %obj.isMethod("getDatablock") ) {
				if( %obj.getDatablock().shapeFile !$= "" )
					%shapePath = %obj.getDatablock().shapeFile;
			}

			//creating toPath
			%k = 0;

			while( strpos( %shapePath, "/", %k ) != -1 ) {
				%pos = strpos( %shapePath, "/", %k );
				%k = %pos + 1;
			}

			%savePath = getSubStr( %shapePath , 0 , %k );
			%savePath = %savePath @ "materials.cs";
			matEd_PersistMan.setDirty(MaterialEditorTools.currentMaterial, %savePath);
		} else {
			matEd_PersistMan.setDirty(MaterialEditorTools.currentMaterial, "art/materials.cs");
		}
	} else
		matEd_PersistMan.setDirty(MaterialEditorTools.currentMaterial);
}
//------------------------------------------------------------------------------

//==============================================================================
function MaterialEditorTools::showSaveDialog( %this, %toMaterial ) {
	LabMsgYesNoCancel("Save Material?",
							"The material " @ MaterialEditorTools.currentMaterial.getName() @ " has unsaved changes. <br>Do you want to save?",
							"MaterialEditorTools.saveDialogSave(" @ %toMaterial @ ");",
							"MaterialEditorTools.saveDialogDontSave(" @ %toMaterial @ ");",
							"MaterialEditorTools.saveDialogCancel();" );
}
//------------------------------------------------------------------------------
//==============================================================================
function MaterialEditorTools::showMaterialChangeSaveDialog( %this, %toMaterial ) {
	%fromMaterial = MaterialEditorTools.currentMaterial;
	LabMsgYesNoCancel("Save Material?",
							"The material " @ %fromMaterial.getName() @ " has unsaved changes. <br>Do you want to save before changing the material?",
							"MaterialEditorTools.saveDialogSave(" @ %toMaterial @ "); MaterialEditorTools.changeMaterial(" @ %fromMaterial @ ", " @ %toMaterial @ ");",
							"MaterialEditorTools.saveDialogDontSave(" @ %toMaterial @ "); MaterialEditorTools.changeMaterial(" @ %fromMaterial @ ", " @ %toMaterial @ ");",
							"MaterialEditorTools.saveDialogCancel();" );
}
//------------------------------------------------------------------------------